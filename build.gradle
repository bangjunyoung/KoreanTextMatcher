plugins {
    id 'java-library'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.assertj:assertj-core:3.27.7'

    testImplementation platform("org.junit:junit-bom:6.0.2")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

version = '3.9999'

subprojects {
    plugins.withId("java-library") {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(17)
            }
        }
    }

    plugins.withId("com.android.library") {
        extensions.configure("android") {
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

javadoc {
    options {
        encoding 'UTF-8'
        noQualifiers 'java.lang'
        links 'https://docs.oracle.com/en/java/javase/17/docs/api/'
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    onlyIf {
        task -> task.project.tasks.test.state.failure == null
    }

    reports {
        xml.required = true
        html.required = true
    }
}

check.dependsOn jacocoTestReport